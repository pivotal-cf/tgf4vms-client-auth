/*
 * Copyright (C) 2017-Present Pivotal Software, Inc. All rights reserved.
 * This program and the accompanying materials are made available under
 * the terms of the under the Apache License, Version 2.0 (the "Licenseâ€);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'java-library'
	id 'maven-publish'
	id "com.diffplug.spotless" version "5.6.1"
}

group = 'io.pivotal.cloudcache'
version '1.13.0'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	// main
	implementation platform('org.apache.geode:geode-all-bom:1.13.0')

	compileOnly 'org.apache.geode:geode-core'


	// test
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
	testImplementation 'org.apache.geode:geode-core'

	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

test {
	useJUnitPlatform()
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java

			pom.withXml {
				def dependenciesNode = asNode().appendNode('dependencies')
				project.configurations.compileOnly.allDependencies.each { dep ->
					dependenciesNode.appendNode('dependency').with {
						it.appendNode('groupId', dep.group)
						it.appendNode('artifactId', dep.name)
						if (dep.version) it.appendNode('version', dep.version)
						it.appendNode('scope', 'provided')
					}
				}
			}
		}
	}
}

spotless {
	groovyGradle {
		target '*.gradle'
		greclipse()
	}

	java {
		googleJavaFormat('1.7').aosp()
	}
}
